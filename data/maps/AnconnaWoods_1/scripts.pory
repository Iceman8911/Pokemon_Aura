mapscripts AnconnaWoods_1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: AnconnaWoods_1_ShowOrHideSpecialPokeballForRiolu_TRANSITION
    MAP_SCRIPT_ON_FRAME_TABLE [
        FLAG_TEMP_9, 0 : AnconnaWoods_1_ShowOrHideSpecialPokeballForRiolu_FRAMETABLE
    ]
}

script AnconnaWoods_1_ShowOrHideSpecialPokeballForRiolu_TRANSITION {  //Deals with the flags/vars
    if(flag(RIOLU_ENCOUNTER)) {  //Player has encountered Riolu
        //hideobjectat(2, MAP_ANCONNA_WOODS_1)
        removeobject(4)
        setflag(FLAG_TEMP_9)
    } else {  //Player hasn't encountered Riolu
        //Since, I'm using VAR_TEMP_9, there isn't really any need to clear it when loading into a map
    }
}

script AnconnaWoods_1_ShowOrHideSpecialPokeballForRiolu_FRAMETABLE {
    addobject(4)
}

script AnconnaWoods_1_BugGem {
    lockall
   # msgbox(FoundBugGem)
    msgbox(format("Why would anyone drop this here?\pWell, Their Loss!"))
    waitmessage
    finditem(ITEM_BUG_GEM)
    releaseall
    end
}

script AnconnaWoods_1_SpecialPokeBallForRiolu {
    lockall
    finditem(ITEM_POKE_BALL)
    msgbox(format("A pokeball...\nCould be useful.."))
    waitmessage
    releaseall
}


script AnconnaWoods_1_Mushrooms {
    lockall
    finditem(ITEM_BIG_MUSHROOM)
    finditem(ITEM_TINY_MUSHROOM, 2)
    msgbox(format("Now that I've found them, I can finally leave."))
    waitmessage
    releaseall
    end
}


script AnconnaWoods_1_Not_Found_Riolu {
    lockall
    #delay(30)
    msgbox(format("I can't go into bushes yet. Wild Pokémon may jump out at me.\pThere has to be another way."))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, NotFoundRiolu)
    waitmovement(0)
    releaseall
}

movement NotFoundRiolu {
    walk_left
}

script AnconnaWoods_1_NotFoundMushrooms {
    lockall
    if(!flag(ANCONNA_WOODS_MUSHROOMS)) {
        msgbox(format("I'm sure those Mushrooms are around here somewhere."))
        waitmessage
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, NotFoundRiolu)
        waitmovement(0)
    }
    releaseall
}

script AnconnaWoods_1_Found_Riolu {
    lock
    delay(30)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerConfused)
    waitmovement(0)
    msgbox(format("Huh?\pWhat is that?"), , "{COLOR GREEN}{PLAYER}")
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerFindsRiolu)
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, WalkToRiolu)
    waitmovement(0)
    delay(60)
    msgbox(format("Um?\p(What's a little Pokémon like this doing in a bug forest?\pOh, it must be that Pokémon those people were looking for.)"), , "{COLOR GREEN}{PLAYER}")
    waitmessage
    closemessage
    applymovement(3, RioluReacts)
    waitmovement(0)
    delay(20)
    msgbox(format("(It looks really scared of me..\pMaybe because of the strange people in the woods)\p{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: How did you get he..."))
    msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Rio Rioo..\pRioluuu!!!"))
    delay(17)
    msgbox(format("Wait!!!"), , "{COLOR GREEN}{PLAYER}")
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, DodgeRiolu)
    waitmovement(0)
    applymovement(3, RioluRamsIntoYou)
    waitmovement(0)
    setvar(VAR_0x8004, 1)  //@ vertical pan
	setvar(VAR_0x8005, 1)  //@ horizontal pan
	setvar(VAR_0x8006, 8)  //@ num shakes
	setvar(VAR_0x8007, 5)  //@ shake delay
    special(ShakeCamera)
    waitstate
    msgbox(format("Woah!\p(Why'd it try to attack me?)"), , "{COLOR GREEN}{PLAYER}")
    waitmessage
    msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Rio lu..."))
    playmoncry(SPECIES_RIOLU, CRY_MODE_ENCOUNTER)
    waitmoncry
    msgbox(format("(It looks like it injured itself though.\pI'd better help it quickly with the potion I brought.)"), , "{COLOR GREEN}{PLAYER}")
    waitmessage
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, TreatRiolu)
    waitmovement(0)
    msgbox(format("Easy does it!"), , "{COLOR GREEN}{PLAYER}")
    delay(35)
    msgbox(format("{PLAYER} used a Potion on the Injured Pokémon!"))
    playfanfare(MUS_HEAL)
    waitfanfare
    delay(15)
    removeitem(ITEM_POTION, 1)
    msgbox(format("Done!"), , "{COLOR GREEN}{PLAYER}")
    applymovement(OBJ_EVENT_ID_PLAYER, StepBackFromRiolu)
    msgbox(format("Doesn't that feel much better?"))
    closemessage
    applymovement(3, RioluTurnsAround)
    waitmovement(0)
    msgbox(format("{COLOR GREEN}Rio..\pRiolu?"))
    playmoncry(SPECIES_RIOLU, CRY_MODE_WEAK)
    waitmoncry
    applymovement(3, RioluApproachesYou)
    waitmovement(0)
    msgbox(format("(It's coming closer again..)"))
    applymovement(3, RioluLikesYou)
    waitmovement(0)
    msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Riolu Riolu!!"))
    waitmessage
    msgbox(format("(Could it be that..)\pDo you wanna come with me.. 'Riolu'?"), , "{COLOR GREEN}{PLAYER}")
    msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Riolu Riolu Riolu!!!"))
    playmoncry(SPECIES_RIOLU, CRY_MODE_NORMAL)
    waitmoncry
    //givemon(SPECIES_RIOLU, 5, ITEM_FOCUS_SASH)
    givecustommon(SPECIES_RIOLU, 5, ITEM_FOCUS_SASH, , NATURE_HASTY, 0, , , , , , , , 31, , 31, 31)
    msgbox(format("Riolu has joined your team!"))
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
    removeitem(ITEM_POKE_BALL)  //gets rid of the pokeball the player found earlier
    setflag(FLAG_SYS_POKEMON_GET)
    delay(40)
    msgbox(format("I'll take that as a yes.\pLets go to my house, Riolu."), , "{COLOR GREEN}{PLAYER}")
    fadescreen(FADE_TO_BLACK)
    closemessage
    removeobject(3)
    delay(50)
    applymovement(OBJ_EVENT_ID_PLAYER, AnconnaWoods_1You)
    waitmovement(0)
    fadescreen(FADE_FROM_BLACK)
    msgbox(format("(The poor little thing is lost, probably because of those people from before.\pLets see if there's anyway I can help.)"))
    setvar(ANCONNA_WOODS_FOUND_RIOLU, 1)
    addvar(GAME_PROGRESS, 1)
    release
}

movement AnconnaWoods_1You {
    walk_in_place_slow_right
}

movement PlayerConfused {
    emote_exclamation_mark
    delay_16
}

movement PlayerFindsRiolu {
    face_right
    step_end
}

movement WalkToRiolu {
    walk_right * 5
    walk_down
    face_right
}

movement RioluReacts {
    face_left
    emote_exclamation_mark
	delay_16
	delay_16
	delay_16
    delay_16
	delay_16
	delay_16
    face_up
	delay_16
	delay_16
	delay_16
    delay_16
	delay_16
	delay_16
    face_down
	delay_16
	delay_16
	delay_16
    delay_16
	delay_16
	delay_16
    face_left
    emote_exclamation_mark
}

movement DodgeRiolu {
    walk_fast_up
    walk_in_place_down
}

movement RioluRamsIntoYou {
    walk_fast_left * 3
}

movement TreatRiolu {
    walk_fast_right
    walk_fast_down
    face_left
}

movement StepBackFromRiolu {
    walk_right
    face_left
}

movement RioluTurnsAround {
    face_right
}

movement RioluApproachesYou {
    walk_right
}

movement RioluLikesYou {
    emote_question_mark
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    emote_heart
}