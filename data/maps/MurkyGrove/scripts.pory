mapscripts MurkyGrove_MapScripts {}

script MurkyGrove_PoisonGem {
    lock
    finditem(ITEM_POISON_GEM)
    release
}

script MurkyGrove_BalmMushroom {
    lock
    finditem(ITEM_BALM_MUSHROOM)
    release
}

script MurkyGrove_Sign {
    lockall
    msgbox(format("Murky Grove\p {LEFT_ARROW} Route 01\p {RIGHT_ARROW} Scandi Town"))
    releaseall
}

script MurkyGrove_BugManiac {
    lockall
    if(!defeated(BUG_MANIAC_BRENT))
    {
        msgbox(format("{COLOR BLUE}There are some really nice stuff here. If you beat me in a battle, I'll tell you what and where they are."), 2)
        bufferdynamicmulti("Sure, I'll take you on!", "Nah, not feeling like it.")
        multichoice(25, 7, MULTI_DYNAMIC_2, 1)
        switch(var(VAR_RESULT))
        {
            // Battle
            case 0:
                trainerbattle_single(BUG_MANIAC_BRENT, format("{COLOR BLUE}Keke, I'm no pushover!"), format("Almost had it there!"))
            case 1:
                msgbox(format("{COLOR BLUE}Perhaps the might of my bugs scared you off ...fufu."))
                waitmessage
            default:
                break
        }
    }
    else
    {
        switch (var(VAR_TEMP_0)) 
        {
        case 0:
            msgbox(format("{COLOR BLUE}Well, a deal's a deal."), 2)
            closemessage
            addvar(VAR_TEMP_0, 1)
        case 1:
            msgbox(format("{COLOR BLUE}To my far right, there's a {COLOR GREEN}Tiny Mushroom{COLOR BLUE} near a little girl."), 2)
            closemessage
            addvar(VAR_TEMP_0, 1)
        case 2:
            msgbox(format("{COLOR BLUE}In a cluster of trees south of here, there's a{COLOR GREEN}Tiny Mushroom{COLOR BLUE} and another a bit futher and close to a ledge."), 2)
            closemessage
            addvar(VAR_TEMP_0, 1)
        case 3:
            if(var(VAR_TEMP_1) == 0) {
                msgbox(format("{COLOR BLUE}The last {COLOR GREEN}Tiny Mushroom{COLOR BLUE} is close to the exit of the grove. You can't miss it!"), 2)
                waitmessage
                closemessage
                addvar(VAR_TEMP_1, 1)
            }
            else
            {
                msgbox(format("{COLOR BLUE}If you want extra loot, go into the cluster of trees and you can find "
                "a {COLOR GREEN}Balm Mushroom{COLOR BLUE}and a {COLOR GREEN}Poison Gem{COLOR BLUE}. There should even be"
                " a {COLOR GREEN}Pok√© Ball{COLOR BLUE} nearby...\pI'd get some of these myself "
                "but I need to tend to my bugs and make them stronger. Be sure to come back later so we can show"
                " how stronger we've gotten!"), 2)
                closemessage
            }
        }
    }
    releaseall
}

script MurkyGrove_AromaLady {
    trainerbattle_single(AROMA_LADY_DAISY, format("{COLOR RED}I normally wouldn't be in a place like this but there are some nice {COLOR GREEN}Balm Mushrooms{COLOR RED} here."), format("This better be worth it."))
    msgbox(format("{COLOR RED}I really want to get out of this place but I want a {COLOR GREEN}Balm Mushroom{COLOR RED} more."), 2)
    closemessage
}

script MurkyGrove_SchoolKid {
    trainerbattle_single(SCHOOL_KID_M_TED, format("{COLOR BLUE}My classmates said that {COLOR GREEN}Grimer{COLOR BLUE} live here.\p Is that true?"), format("Gotta get that Grimer!"))
    msgbox(format("{COLOR BLUE}I'm pretty sure that {COLOR GREEN}Grimer{COLOR BLUE} live here. I'll keep looking."), 2)
    waitmessage
    msgbox(format("Why do you want a {COLOR GREEN}Grimer{COLOR DARK_GRAY} anyways?"), 2, "{COLOR GREEN}{PLAYER}")
    waitmessage
    msgbox(format("{COLOR BLUE}There's an inside joke with my schoolmates about {COLOR GREEN}Muk{COLOR BLUE} and I don't quite"
    " understand what its all about so getting a {COLOR GREEN}Grimer{COLOR BLUE} first might help."), 2)
    waitmessage
    msgbox(format("Uhhh.. I doubt you'll wanna know anyways, cya."), , "{COLOR GREEN}{PLAYER}")
    closemessage
}

script MurkyGrove_YoungGirl {
    msgbox(format("{COLOR RED}The ground here is so mucky, I bet you can't even run or bike in it."), 2)
    closemessage
}