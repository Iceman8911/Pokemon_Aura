mapscripts AnconnaTown_SiblingsRoom_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(RIOLU_IS_IN_PLAYER_HOUSE) //Prevents Riolu from showing until the special cutscene
    }
}

//I'm using trigger to find where the player is standing
script Anconna_Town_PlayerIsFacingUp {
    setvar(VAR_TEMP_8, 1) //Facing Up To Sister
    setvar(VAR_TEMP_9, 1) //U are currently standing on trigger
    setvar(VAR_TEMP_2, 0)
}

script Anconna_Town_PlayerIsFacingLeft {
    setvar(VAR_TEMP_8, 2) //Facing Left To Sister
    setvar(VAR_TEMP_9, 1) //U are currently standing on trigger
    setvar(VAR_TEMP_2, 0)
}

script Anconna_Town_PlayerIsFacingRight {
    setvar(VAR_TEMP_8, 3) //Facing Right To Sister
    setvar(VAR_TEMP_9, 1) //U are currently standing on trigger
    setvar(VAR_TEMP_2, 0)
}

script ResetVar_Temp_9 {
    setvar(VAR_TEMP_9, 0) //To show tht u are not asking her
    setvar(VAR_TEMP_2, 1)
}

script SisterBidoofAtHome {
    lockall
    faceplayer
    applymovement(2, Happy)
    playmoncry(SPECIES_BIDOOF, CRY_MODE_NORMAL)
    waitmoncry
    msgbox(format("{COLOR GREEN}Bibby: Bi Dooooff...\p{COLOR DARK_GRAY}(Bidoof looks very snug and happy)"), 2)
    releaseall
}

script Anconna_Town_SisterAtHome {
    lock
    switch(var(VAR_TEMP_0)) 
    {
        case 0:
        call(Anconna_TownSiblingMainScript) //Main script
        case 1:
        call(Anconna_Town_SisterLostSomething_1) //Quest script
    }
    release
}

script Anconna_TownSiblingMainScript {
    if(var(GAME_PROGRESS) < 6) 
    { //If you just entered the room for the first time
        msgbox(format("{SIBLING}:{COLOR RED} Where is it?\p...\pWHERE THE HECK COULD THIS THING BE?"))
        waitmessage
        msgbox(format("(It does't seem like she noticed me, maybe I should try calling her again...)"))
        waitmessage
        addvar(VAR_TEMP_0, 1)
    } 
    elif(var(GAME_PROGRESS) == 6) 
    { //Asking If you have found Her Bidoof's Comb

        random(2)  //She can ask 2 different things
        faceplayer
        switch(var(VAR_RESULT)) {
            case 0:
                msgbox(format("{SIBLING}:{COLOR RED} Did you find it yet?"))
            case 1:
                msgbox(format("{SIBLING}:{COLOR RED} Have you found it yet?"))
        }
        bufferdynamicmulti("Yeah", "Not yet..")  //loading the multichoice that will be used to answer her
        multichoice(25, 7, MULTI_DYNAMIC_2, 1)
        msgbox(".....")  //shows that she's checking you
        waitmessage
        delay(50)
        call(AnconnaTownSiblingWalksInPlace)
        switch(var(VAR_RESULT)) 
        {
            case 0:    //You chose "Yeah"
            if(var(VAR_RESULT) != 7) 
            {        //checking if you lied or not
                checkitem(ITEM_BIDOOF_COMB, 1)
                if(var(VAR_RESULT)){   ///You found it :D
                    msgbox(format("{SIBLING}:{COLOR RED} Yay :D!!!\nYou found it!"))
                    removeitem(ITEM_BIDOOF_COMB, 1)
                    playfanfare(MUS_OBTAIN_ITEM)
                    msgbox(format("You returned the Ugly Comb!"))
                    waitmessage
                    waitfanfare
                    delay(25)
                    call(AnconnaTownSiblingWalksInPlace)
                    msgbox(format("{SIBLING}:{COLOR RED} By the way, where did you find it?\pIt was a secret gift I made for {COLOR GREEN}Bibby{COLOR RED}"
                    "so I have no idea how it got lost..."))
                    call(AnconnaTownPlayerWalksInPlace)
                    msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: Well..."))
                    bufferdynamicmulti("It was somewhere near the staircase..", "It was in the trash bin..")
                    call(PlayerTellsSiblingAboutBidoofComb)
                    call(AnconnaTownSiblingWalksInPlace)
                    msgbox(format("{SIBLING}:{COLOR RED} Anyways, I believe you wanted to show me something since you came here in the first place."))
                    call(AnconnaTownPlayerWalksInPlace)
                    msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: Yeah, about that.."))
                    closemessage
                    switch(var(VAR_TEMP_8)) { //Controls the player movement right before showing Riolu
                        case 1:
                        applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
                        case 2:
                        applymovement(OBJ_EVENT_ID_PLAYER, PlayerMovesRightThenDown)
                        waitmovement(0)
                        applymovement(OBJ_EVENT_ID_PLAYER, FaceUp)
                        case 3:
                        applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
                    }
                    waitmovement(0)
                    msgbox(format("You can come out now, Riolu."))
                    fadescreen(FADE_TO_WHITE)
                    call(SpawnRioluInSiblingsRoom)                    
                    switch(var(VAR_TEMP_8)) { //Controls ur Siblings "surprised" movement
                        //case 0:
                        case 1:
                        applymovement(1, SiblingIsSurprised1)
                        case 2:
                        applymovement(1, SiblingIsSurprised2)
                        case 3:
                        applymovement(1, SiblingIsSurprised3)
                    }
                    waitmovement(0)
                    msgbox(format("{SIBLING}:{COLOR RED} Eeek!\pYou got yourself a Pokemon! And its really cute :3\pAww, lemme hug it!"))
                    waitmessage
                    delay(60)
                    call(RioluPunchesSiblingLol)
                    switch(var(VAR_TEMP_8)) { //Controls ur Riolu Punch
                        //case 0:
                        case 1:
                        applymovement(3, RioluJumpsAndTriesToPunchesSibling1)
                        waitmovement(0)
                        applymovement(1, SiblingFlies1)
                        waitmovement(0)
                        case 2:
                        applymovement(3, RioluJumpsAndTriesToPunchesSibling2)
                        waitmovement(0)
                        applymovement(1, SiblingFlies2)
                        waitmovement(0)
                        case 3:
                        applymovement(3, RioluJumpsAndTriesToPunchesSibling3)
                        waitmovement(0)
                        applymovement(1, SiblingFlies3)
                        waitmovement(0)
                    }
                    delay(45)
                    msgbox(format("{SIBLING}:{COLOR RED} Ouch :(\pI just wanted to hug it."))
                    waitmessage
                    delay(45)
                    switch(var(VAR_TEMP_8)) {  //controls how she moves after getting it by Riolu
                        case 1:
                        applymovement(1, Anconna_TownSiblingWalksTowardsPlayer1)
                        case 2:
                        applymovement(1, Walk_Right)
                        case 3:
                        applymovement(1, Anconna_TownSiblingWalksTowardsPlayer3)
                    }
                    waitmovement(0)
                    msgbox(format("{SIBLING}:{COLOR RED} Its really strong though so take care of the little guy.\pSince {COLOR GREEN}Bibby{COLOR RED} and I don't train much, I'd like you both to have this. Take it as a reward for helping me earlier."))
                    waitmessage
                    giveitem(ITEM_RARE_CANDY, 1)
                    msgbox(format("{SIBLING}:{COLOR RED} Thanks again for helping me find {COLOR GREEN}Bibby's{COLOR RED} comb. Now I can make him look all pretty again."))
                    waitmessage
                    fadescreen(FADE_TO_BLACK)  //After talking with sibling, I need to clear some stuff
                    removeobject(3)
                    applymovement(1, FaceUp)
                    waitmovement(0)
                    fadescreen(FADE_FROM_BLACK)  //Done 
                    applymovement(OBJ_EVENT_ID_PLAYER, Walk_Down) //Style :)
                    waitmovement(0)
                    completequest(QUEST_FIND_BIBBYS_COMB)
                    addvar(GAME_PROGRESS, 1)
                    
                } else {
                    msgbox(format("{SIBLING}:{COLOR RED} If you didn't find it, why did you still say yes?"), 2)    //You lied
                    waitmessage
                }
            } else {
                //Nothing
            }
            
            case 1:   //You chose "Not yet.."
            msgbox(format("{SIBLING}:{COLOR RED} Try checking {COLOR GREEN}everywhere{COLOR RED} again. Who knows where it might be..."), 2)
            waitmessage
            break
        }
    } else 
    {
        msgbox(format("{SIBLING}:{COLOR RED} Hmm, what should {COLOR GREEN}Bibby{COLOR RED} and I do..."))
        waitmessage
    }
    return
}

script Anconna_Town_SisterLostSomething_1 {
    msgbox(format("(How should I call her...)"))
    bufferdynamicmulti("{SIBLING}!", "HELLO?!")
    multichoice(25, 7, MULTI_DYNAMIC_2, 1)
    switch(var(VAR_TEMP_8)) {
        //case 0:
        case 1:  //she'll face down
            applymovement(1, FaceDown)
            waitmovement(0)
        case 2:  //she'll face right
            applymovement(1, FaceRight)
            waitmovement(0)
        case 3:  //she'll face left
            applymovement(1, FaceLeft)
            waitmovement(0)
    }
    msgbox(format("{SIBLING}:{COLOR RED} WHAT DO YOU WANT?!!"))
    waitmessage
    applymovement(1, Surprised)
    waitmovement(0)
    msgbox(format("{SIBLING}:{COLOR RED} Oh\pI'm sorry about that. Its just that I'm in an awful mood now."
    "\pI lost {COLOR GREEN}Bibby's{COLOR RED} new hair brush and I can't bare to tell him because it'll crush his little heart."
    "\pJust look at how happy he is now."))
    waitmessage
    closemessage
    applymovement(1, FaceDown) //both of you look at bibby
    applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
    waitmovement(0)
    delay(70)
    applymovement(2, AnconnaTown_BibbyTurnsAround)
    waitmovement(0)
    msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: Um, he looks norm-"))
    call(AnconnaTownPlayerWalksInPlace)
    waitmovement(0)
    msgbox(format("{SIBLING}:{COLOR RED} He might look like normal now but once he finds out..."))
    waitmessage
    closemessage
    delay(30)
    applymovement(1, FaceUp)
    waitmovement(0)
    delay(30)
    msgbox(format("{COLOR RED}Sob sob..."))
    waitmessage
    delay(60)
    msgbox(format("Sigh.."))
    
    switch(var(VAR_TEMP_8)) {
        //case 0:
        case 1: //Player was facing up originally
            applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceUp)
            waitmovement(0)
            msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: How about I search downstai-"))
            waitmessage
            applymovement(1, Surprised)
            waitmovement(0)
            applymovement(1, FaceDown)
        case 2: //Player was facing left originally
            applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceLeft)
            waitmovement(0)
            msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: How about I search downstai-"))
            waitmessage
            applymovement(1, Surprised)
            waitmovement(0)
            applymovement(1, FaceRight)
        case 3: //Player was facing right originally
        applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("{COLOR GREEN}{PLAYER}{COLOR DARK_GRAY}: How about I search downstai-"))
            waitmessage
            applymovement(1, Surprised)
            waitmovement(0)
            applymovement(1, FaceLeft)
    }
    waitmovement(0)
    msgbox(format("{SIBLING}:{COLOR RED} Thanks :D\pI'll keep on looking upstairs while you look downstairs."))
    waitmessage
    closemessage
    applymovement(1, FaceUp)
    waitmovement(0)
    delay(30)
    switch(var(VAR_TEMP_8)){
        case 1:
        case 3:
        applymovement(OBJ_EVENT_ID_PLAYER, Walk_Down)
        case 2:
        applymovement(OBJ_EVENT_ID_PLAYER, Walk_Right) //specially done bcus of some tile issues
    }
    delay(30)
    msgbox(format("Why do I feel like I've been set up again.."))
    setflag(FLAG_SYS_QUEST_MENU_GET)  //Enable Quests
    call(FindBibbyCombQuest)//need to activate a quest
    addvar(GAME_PROGRESS, 2) //Progress In The Story. Made a mistake earlier so GAME_PROGRESS = 5 is skipped hence the +2 here
    setvar(VAR_TEMP_0, 0)
    return
}

script FindBibbyCombQuest {
    delay(25)
    playfanfare(MUS_OBTAIN_BADGE)
    msgbox(format("You have unlocked a New Quest!\pYou can check it out in the Quests Section in the menu."))
    waitfanfare
    questmenu(QUEST_MENU_SET_ACTIVE, QUEST_FIND_BIBBYS_COMB)
    return
}


















script AnconnaTownPlayerWalksInPlace {
    switch(var(VAR_TEMP_8)) //Controls ur movement direction depending on where u stand
    {
        case 1:  
            applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceUp)
            waitmovement(0)
        case 2: 
            applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceLeft)
            waitmovement(0)
        case 3:  
            applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceRight)
            waitmovement(0)
    }
    return
}

script AnconnaTownSiblingWalksInPlace {
    switch(var(VAR_TEMP_8)) //Decides how the sibling moves depending on where u stand
    {
        case 1:  //She'll face down
            applymovement(1, WalkInPlaceDown)
            waitmovement(0)
        case 2:  //She'll face right
            applymovement(1, WalkInPlaceRight)
            waitmovement(0)
        case 3:  //She'll face left
            applymovement(1, WalkInPlaceLeft)
            waitmovement(0)
    }
    return
}

script PlayerTellsSiblingAboutBidoofComb {
    multichoice(25, 7, MULTI_DYNAMIC_2, 1)
    switch(var(VAR_RESULT))
    {
    case 0:
    msgbox(format("{SIBLING}:{COLOR RED} Huh, I have no idea how it got there.")) 
    case 1:
    msgbox(format("{SIBLING}:{COLOR RED} ......\pPffffft! That's impossible!\nBe serious, where did you actually find it?"))
    call(PlayerTellsSiblingAboutBidoofComb) //This will cause the multichoice to loop until you chose the first option
    }
    return
}

script SpawnRioluInSiblingsRoom {      //Spawns Riolu during a cutscene
    clearflag(RIOLU_IS_IN_PLAYER_HOUSE)
    addobject(3)
    switch(var(VAR_TEMP_8)){
        case 0:
        case 1: //facing up
        setobjectxy(3, 9, 7)
        fadescreen(FADE_FROM_WHITE)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceUp)
        applymovement(3, WalkInPlaceUp)
        case 2: //facing left
        setobjectxy(3, 11, 6)
        fadescreen(FADE_FROM_WHITE)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
        applymovement(3, WalkInPlaceLeft)
        case 3: //facing right
        setobjectxy(3, 9, 7)
        fadescreen(FADE_FROM_WHITE)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
        applymovement(3, WalkInPlaceRight)
    }
    waitmovement(0)
    playmoncry(SPECIES_RIOLU, CRY_MODE_NORMAL)
    msgbox(format("{COLOR GREEN}Rioluuuuuuu"))
    waitmoncry
    return
}

script RioluPunchesSiblingLol {    //Controls Riolu punching your stupid sibling
    switch(var(VAR_TEMP_8)) {
        case 0:
        case 1:
        applymovement(1, WalkLeftThenFaceDown)
        waitmovement(0)
        delay(60)
        msgbox(format("{SIBLING}:{COLOR RED} So warm..."))
        waitmessage
        playmoncry(SPECIES_RIOLU, CRY_MODE_NORMAL)
        msgbox(format("{COLOR GREEN}Riooluuuu!"))
        waitmoncry
        case 2:
        //applymovement(1, Walk_Right)
        //waitmovement(0)
        msgbox(format("{SIBLING}:{COLOR RED} So warm..."))
        waitmessage
        playmoncry(SPECIES_RIOLU, CRY_MODE_NORMAL)
        msgbox(format("{COLOR GREEN}Riooluuuu!"))
        waitmoncry
        case 3:
        applymovement(1, WalkDownThenFaceLeft)
        waitmovement(0)
        msgbox(format("{SIBLING}:{COLOR RED} So warm..."))
        waitmessage
        playmoncry(SPECIES_RIOLU, CRY_MODE_NORMAL)
        msgbox(format("{COLOR GREEN}Riooluuuu!"))
        waitmoncry
    }
}


movement WalkInPlaceLeft {
    walk_in_place_left
}

movement WalkInPlaceRight {
    walk_in_place_right
}

movement AnconnaTown_BibbyTurnsAround {
    emote_exclamation_mark
    delay_16
    delay_16
    delay_16
    face_right
    delay_16
    face_left
    delay_16
    face_right
}

movement Walk_Right {
    walk_right
}
movement PlayerMovesRightThenDown {
    walk_right
    walk_down
}

movement SiblingIsSurprised1 {
    emote_exclamation_mark
    delay_16
    delay_16
    jump_in_place_down
}

movement SiblingIsSurprised2 {
    emote_exclamation_mark
    delay_16
    delay_16
    jump_in_place_right
}

movement SiblingIsSurprised3 {
    emote_exclamation_mark
    delay_16
    delay_16
    jump_in_place_left
}

movement WalkLeftThenFaceDown {
    walk_left
    face_down
}

movement WalkDownThenFaceLeft {
    walk_down
    face_left
}

movement RioluJumpsAndTriesToPunchesSibling1{
    jump_in_place_up
    delay_16
}

movement RioluJumpsAndTriesToPunchesSibling2{
    jump_in_place_left
    delay_16
}

movement RioluJumpsAndTriesToPunchesSibling3{
    jump_in_place_right
    delay_16
}

movement SiblingFlies1 {
    lock_facing_direction
    jump_up
    unlock_facing_direction
}

movement SiblingFlies2 {
    lock_facing_direction
    jump_left
    unlock_facing_direction
}

movement SiblingFlies3 {
    lock_facing_direction
    jump_right
    unlock_facing_direction
}

movement Anconna_TownSiblingWalksTowardsPlayer1 {
    walk_down
    walk_right
    face_player
}

movement Anconna_TownSiblingWalksTowardsPlayer2 {  //unused
    walk_up
    walk_left
    face_player
}

movement Anconna_TownSiblingWalksTowardsPlayer3 {
    walk_up
    walk_left
    face_player
}

